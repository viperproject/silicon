//:: IgnoreFile(/silicon-wands/issue/9/)

/* It encodes and specifies an iterative computation of the sum
 * of a recursively defined linked-list. Magic wands are used to book-keep
 * permissions in the loop.
 */

field val: Int
field next: Ref

/* Linked-list abstract predicate. Contains access to the fields of the current
 * node ys, and transitively, to the fields of all nodes in the tail.
 */
predicate List(ys: Ref) {
  acc(ys.val) && acc(ys.next) && (ys.next != null ==> acc(List(ys.next)))
}

/* Pure function that computes the sum in a straight-forward, recursive way. */
function sum_rec(ys: Ref): Int
  requires acc(List(ys))
{ unfolding List(ys) in ys.val + (ys.next == null ? 0 : sum_rec(ys.next)) }

/* Iterative computation of the sum over the linked-list ys. The postcondition
 * states that the iterative computation yields the same result as the
 * recursive one.
 */
method sum_it(ys: Ref) returns (sum: Int)
  requires ys != null
  requires acc(List(ys))
  ensures acc(List(ys))
  ensures sum == old(sum_rec(ys))
{
  var xs: Ref := ys /* Pointer to the current node in the list */
  sum := 0          /* Sum computed so far*/

  /* Short-hands to keep the specifications concise */
  define A xs != null ==> acc(List(xs)) && sum_rec(xs) == now(sum_rec(xs))
  define B acc(List(ys)) && sum_rec(ys) == old(sum_rec(ys))

  /* (Trivially) establish the promise that giving up the list starting from
   * the current node xs - which at this point is the head of the list - in a
   * shape s.t. its sum is the same as it is at this point
   * (sum_rec(xs) == now(sum_rec(xs)), yields a list that starts at the old
   * head node (ys) and whose sum is the sum of the original list
   * (sum_rec(ys) == old(sum_rec(ys))).
   */
  package A --* B

  /* Iteratively compute the sum.
   * The loop invariant states that
   *  1. we have permissions to the list starting at the current node xs (if
   *     not null)
   *  2. the computed sum is the sum of the original list minus the sum of the
   *     nodes still to visit, i.e., the sum of the list starting at the
   *     current node xs.
   *  3. the previously described promise holds
   */
  while (xs != null)
    invariant xs != null ==> acc(List(xs))
    invariant sum == old(sum_rec(ys)) - (xs == null ? 0 : sum_rec(xs));
    invariant A --* B
  {
    /* Bind w to the magic wand instance that we currently hold in our state */
    wand w := A --* B

    /* Let zs point to the *current* current node, update the sum and advance
     * the current node pointer.
     */
    var zs: Ref := xs
    unfold List(xs)
    sum := sum + xs.val
    xs := xs.next

    /* Update the promise. This exhales access to the fields of the *previous*
     * current node zs, and the magic wand instance w, since they belong to the
     * footprint of the packaged magic wand instance.
     */
    package A --* folding List(zs) in applying w in B
  }

  /* Use the promise to get back the unchanged list starting at ys.
   * xs is null, so nothing is given up, but that is correct because
   * intuitively, the permissions to each node in the list has been
   * packaged into the wand while the loop executed.
   */
  apply A --* B
}
